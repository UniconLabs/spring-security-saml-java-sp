defaultTasks "build", "jettyRunWar"

allprojects {
    repositories {
        maven { url 'https://build.shibboleth.net/nexus/content/groups/public'}
        jcenter()
        mavenLocal()
    }
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.akhikhl.gretty:gretty:1.2.4"
    }
}

apply plugin: 'war'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: "org.akhikhl.gretty"

war.archiveName = "sp.war"

description = "Spring Security SAML v2 sample webapp"

dependencies {
    compile "org.springframework.security.extensions:spring-security-saml2-core:${project.'spring-security-saml2.version'}",
        "org.springframework:spring-core:${project.'spring.version'}",
        "org.springframework:spring-beans:${project.'spring.version'}",
        "org.springframework:spring-context:${project.'spring.version'}",
        "org.springframework:spring-aop:${project.'spring.version'}",
        "org.springframework:spring-web:${project.'spring.version'}",
        "org.springframework:spring-webmvc:${project.'spring.version'}",
        "org.springframework.security:spring-security-config:${project.'spring-security.version'}",
        "org.slf4j:slf4j-log4j12:${project.'slf4j.version'}",
        "javax.servlet:jstl:1.2"

    providedCompile "javax.servlet:jsp-api:2.0"

    testCompile "junit:junit:4.4"
}

configurations {
    grettyRunnerJetty9 {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.ow2.asm') {
                details.useVersion('5.0.3')
            }
            if (details.requested.group == 'org.glassfish'
                    && details.requested.name == 'javax.el') {
                details.useVersion('3.0.0')
            }
        }
    }
}

gretty {
    jvmArgs = ["-Dorg.eclipse.jetty.annotations.maxWait=120","-Xdebug","-Xrunjdwp:transport=dt_socket,address=5005,server=y,suspend=n"]
    scanInterval = 5
    contextPath = '/sp'
    httpsEnabled = true
    httpsPort = 9443
    httpPort = 8081
    sslKeyStorePath = 'c:\\etc\\cas\\jetty\\thekeystore'
    sslKeyStorePassword = 'changeit'
    sslTrustStorePath = 'c:\\etc\\cas\\jetty\\thekeystore'
    sslKeyStorePassword = 'changeit'
}

